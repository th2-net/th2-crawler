plugins {
    id 'java'
    id 'org.jetbrains.kotlin.jvm' version "$kotlin_version"
    id 'com.palantir.docker' version '0.25.0'
    id 'application'
    id 'org.jetbrains.kotlin.kapt' version "$kotlin_version"
}

group 'com.exactpro.th2'
version release_version

sourceCompatibility = JavaVersion.VERSION_11
targetCompatibility = JavaVersion.VERSION_11

compileKotlin {
    kotlinOptions {
        jvmTarget = "11"
    }
}

compileTestKotlin {
    kotlinOptions {
        jvmTarget = "11"
    }
}

ext {
    cradleVersion = '3.2.2-TH2-3789-2508815656-SNAPSHOT'
    grpcCrawlerVersion = '0.3.0-TH2-4262-reduce-load-on-a-separate-boxes-in-crawler-schema-SNAPSHOT'
//    grpcCrawlerVersion = '0.3.0'
    sharedDir = file("${project.rootDir}/shared")
}

dependencies {
    // BOM with external dependencies restrictions
    api platform('com.exactpro.th2:bom:3.3.0-TH2-3093-3052871635-SNAPSHOT')

    api "com.exactpro.th2:cradle-core:${cradleVersion}"

    implementation "com.exactpro.th2:cradle-cassandra:${cradleVersion}"

    // the library for interaction with infra API
    implementation 'com.exactpro.th2:common:3.41.0-TH2-4188-100-msg-sec-throughput-in-end-to-end-test-3133763167-SNAPSHOT'

    implementation "com.exactpro.th2:grpc-crawler-data-processor:${grpcCrawlerVersion}"

//    implementation ('com.exactpro.th2:grpc-crawler-data-processor') {
//        version {//FIXME: remove after encapsulate into grpc-crawler-data-processor
//            strictly '0.3.0-TH2-4262-reduce-load-on-a-separate-boxes-in-crawler-schema-SNAPSHOT'
//        }
//    }

    //    implementation ('com.exactpro.th2:grpc-data-provider:1.1.0')
    implementation ('com.exactpro.th2:grpc-data-provider') {
        version {//FIXME: remove after encapsulate into grpc-crawler-data-processor
            strictly '1.1.0-TH2-4262-reduce-load-on-a-separate-boxes-in-crawler-schema-3218351603-SNAPSHOT'
        }
    }

    implementation ("com.exactpro.th2:grpc-service-generator") {
        version {//FIXME: this structure will not be required after migration to release version
            strictly '3.2.2-TH2-4262-reduce-load-on-a-separate-boxes-in-crawler-schema-3197511115-SNAPSHOT'
        }
    }
    implementation("com.fasterxml.jackson.datatype:jackson-datatype-jsr310") {
        because('we need to process java Instant class')
    }
    implementation('com.fasterxml.jackson.module:jackson-module-kotlin') {
        because('we need to be able crate a mapper for state deserialization')
    }

    // Logging. All versions are specified in BOM dependency
    implementation "org.slf4j:slf4j-api"

    // Idiomatic logging for Kotlin. Wraps slf4j
    implementation 'io.github.microutils:kotlin-logging:2.0.4'

    implementation "org.jetbrains.kotlin:kotlin-stdlib"
    implementation("org.jetbrains.kotlin:kotlin-reflect") {
        because('the old kotlin version in classpath')
    }

    implementation('io.prometheus:simpleclient') {
        because('metrics to export')
    }

    implementation('io.grpc:grpc-core') {
        because('default max message size constant')
    }

    compileOnly 'com.google.auto.service:auto-service-annotations:1.0'
    kapt 'com.google.auto.service:auto-service:1.0'
    annotationProcessor 'com.google.auto.service:auto-service:1.0'

    testImplementation 'org.junit.jupiter:junit-jupiter:5.6.0'
    testImplementation 'org.jetbrains.kotlin:kotlin-test-junit'
    testImplementation "io.grpc:grpc-netty"

    // https://mvnrepository.com/artifact/org.mockito/mockito-core
    testImplementation group: 'org.mockito', name: 'mockito-core', version: '3.11.2'
}

test {
    useJUnitPlatform()
    testLogging {
        showStandardStreams = true
    }
}

jar {
    manifest {
        attributes(
                'Created-By':               "${System.getProperty('java.version')} (${System.getProperty('java.vendor')})",
                'Specification-Title':      '',
                'Specification-Vendor':     'Exactpro Systems LLC',
                'Implementation-Title':     project.archivesBaseName,
                'Implementation-Vendor':    'Exactpro Systems LLC',
                'Implementation-Vendor-Id': 'com.exactpro',
                'Implementation-Version':   project.version
        )
    }
}

application {
    mainClass.set('com.exactpro.th2.crawler.main.Main')
}

applicationName = 'service'

distTar {
    archiveFileName.set("${applicationName}.tar")
}

dockerPrepare {
    dependsOn distTar
}

docker {
    copySpec.from(tarTree("$buildDir/distributions/${applicationName}.tar"))
}